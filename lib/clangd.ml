open Core

let to_compile_flags_txt (args : Builder.Compiler_args.t) =
  let module C = Builder.Compiler_args in
  let target =
    let open Project_config in
    let convert_mcu mcu =
      let pat = Re.compile @@ Re.Pcre.re {|(\d+\w*)+$|} in
      let match' = List.hd_exn @@ Re.matches pat mcu in

      String.lowercase mcu
      |> String.substr_replace_first ~pattern:"at" ~with_:"AT"
      |> String.substr_replace_first ~pattern:match'
           ~with_:(String.uppercase match')
      |> sprintf "__AVR_%s__"
    in

    let f = function
      | { mcu; hz = Some hz } ->
          [ sprintf "-D%s" (convert_mcu mcu); sprintf "-DF_CPU=%d" hz ]
      | { mcu; hz = None } -> [ sprintf "-D%s" (convert_mcu mcu) ]
    in
    Option.value_map ~default:[] ~f args.target
  in

  List.concat
    [
      [ "# This file is generated by bavar, edit LabAvrProject instead" ];
      Builder.strict_flags;
      target;
      C.of_build_options args.build_options;
      C.of_headers args.headers;
      [ "-I"; "/usr/avr/include" ] (* FIXME: hardcode paths *);
      C.to_includes args.includes;
    ]
